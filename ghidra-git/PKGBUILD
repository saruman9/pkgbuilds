# Maintainer: Alex Sarum <rum.274.4 at gmail dot com>

_pkgname='ghidra'
pkgname=${_pkgname}-git
pkgver=9.2.r150.gfde10249e
pkgrel=1
pkgdesc="Software reverse engineering framework (git)"
arch=('x86_64')
url='https://www.nsa.gov/ghidra'
license=('Apache')
provides=('ghidra')
conflicts=('ghidra' 'ghidra-bin')
depends=('java-environment>=11' 'hicolor-icon-theme')
makedepends=('git' 'gradle' 'unzip')
optdepends=('python2: for the Python scripting feature')
source=("$pkgname::git+https://github.com/saruman9/ghidra#branch=dev"
        "git+https://github.com/0x6d696368/ghidra-data"
        "git+https://github.com/threatrack/ghidra-fidb-repo"
        ghidra.desktop)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'a85b8b3276e2ff4ed8bda6470c15d02711ebaa48463c775cd2a36549fad738e9fe073dab80f8c57646490ffc959cdc27e9d25b1dc2a5810b0ddb249b5dc99a9b')

pkgver() {
  cd $srcdir/$pkgname
  git describe --tags --long | sed 's/_build//;s/Ghidra_//;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $srcdir/$pkgname

  sed -Ei 's/(application\.version=).*$/\10.1/;s/(application\.release\.name=).*$/\1GIT/' Ghidra/application.properties

  gradle --init-script gradle/support/fetchDependencies.gradle init
}

build() {
  cd $srcdir/$pkgname

  gradle buildGhidra
}

package() {
  cd $srcdir/$pkgname

  install -d $pkgdir/{opt,usr/bin}

  # Extract built archive into destination folder
  _appver=$(grep -oP '(?<=^application.version=).*$' Ghidra/application.properties)
  _relname=$(grep -oP '(?<=^application.release.name=).*$' Ghidra/application.properties)
  unzip -u build/dist/${_pkgname}_${_appver}_${_relname}_$(date +"%Y%m%d")_linux64.zip -d $pkgdir/opt
  # Use a simple directory name
  mv $pkgdir/opt/${_pkgname}{_${_appver}_${_relname},}

  # Add FID datasets
  install -Dm 644 $srcdir/${_pkgname}-data/FunctionID/*.fidb -t $pkgdir/opt/${_pkgname}/Ghidra/Features/FunctionID/data/
  install -Dm 644 $srcdir/${_pkgname}-fidb-repo/*.fidb -t $pkgdir/opt/${_pkgname}/Ghidra/Features/FunctionID/data

  # Add GDT
  install -Dm 644 $srcdir/${_pkgname}-data/typeinfo/*.gdt -t $pkgdir/opt/${_pkgname}/Ghidra/Features/Base/data/typeinfo/custom/

  # Create custom username
  sed -i '0,/VMARGS/!b;//a\\n# Username\nVMARGS=-Duser.name='"$USER"'' $pkgdir/opt/${_pkgname}/support/launch.properties

  ln -s /opt/${_pkgname}/ghidraRun $pkgdir/usr/bin/${_pkgname}
  ln -s /opt/${_pkgname}/support/analyzeHeadless $pkgdir/usr/bin/${_pkgname}-analyzeHeadless

  install -Dm 644 $srcdir/ghidra.desktop -t $pkgdir/usr/share/applications
  for i in 16 24 32 40 48 64 128 256; do
    install -Dm 644 Ghidra/Framework/Generic/src/main/resources/images/GhidraIcon$i.png \
      $pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/ghidra.png
  done

  install -Dm 644 LICENSE -t $pkgdir/usr/share/licenses/${_pkgname}
}
