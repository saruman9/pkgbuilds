# Maintainer: Alex Sarum <rum.274.4 at gmail dot com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=uefi-firmware-parser-git
pkgver=1.8.r13.ga82d386
pkgrel=1
pkgdesc='Parse BIOS/Intel ME/UEFI firmware related structures: Volumes, FileSystems, Files, etc'
arch=('any')
license=(MIT)
url='https://gitlab.com/saruman9/uefi-firmware-parser'
depends=(python)
provides=('uefi-firmware-parser')
conflicts=('uefi-firmware-parser')
makedepends=(python-setuptools)
source=(${pkgname}::git+${url})
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
  cd "${srcdir}/${pkgname}"
  python setup.py install --root="$pkgdir" --optimize=1

  install -m755 -d "$pkgdir/usr/bin"
  install -m755 -t "$pkgdir/usr/bin" scripts/{fv_injector.py,fv_parser.py,uefi_guids.py,contrib/dell_extract_hdr.py}
  install -m 644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
