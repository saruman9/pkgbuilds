# Maintainer: Alex Sarum <rum.274.4 at gmail dot com>

pkgname=imhex-git
pkgver=1.7.0.r87.g0168a3b
pkgrel=1
pkgdesc="A Hex Editor for Reverse Engineers, Programmers and people that value their eye sight when working at 3 AM"
arch=('x86_64')
url="https://github.com/WerWolv/ImHex"
license=('GPL2')
depends=('glfw' 'glm' 'capstone' 'llvm-libs' 'llvm' 'nlohmann-json' 'python')
makedepends=('git' 'cmake')
optdepends=()
provides=('imhex')
conflicts=('imhex')
source=("$pkgname::git+https://github.com/WerWolv/ImHex.git")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname"
  git submodule update --init --recursive
}

build() {
  cd "$pkgname"
  cmake -S . -B build -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  cmake --build build -j $(nproc)
}

package() {
  cd "$pkgname"

  install -DTm755 build/imhex "${pkgdir}/usr/bin/imhex"
  install -DTm755 build/plugins/libimhex/libimhex.so "${pkgdir}/usr/lib/libimhex.so"
}
